apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'//添加

android {
    compileSdkVersion 27



    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //更新下载相关模块（基于HttpURLConnection）
    api('com.bodhi.updcore:updcore:1.0.0') {
        exclude group: 'com.android.support'
    }

    //网络请求相关模块(基于OkHttp)
    api('com.bodhi.httpcore:httpcore:1.0.0') {
        exclude group: 'com.android.support'
    }

    //图片加载相关模块（基于glide）
    api 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    //状态栏相关依赖
    api('com.jaeger.statusbarutil:library:1.5.0') {
        exclude group: 'com.android.support'
    }
}


repositories {
    mavenCentral()
}

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//生成Javadoc文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到JCenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

//解决 JavaDoc 中文注释生成失败的问题
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

//发布到 Bintray
publish {

    userOrg = 'bodhisun'           //bintray.com 注册的用户名
    groupId = 'com.bodhi.framecore'   //以后访问 jcenter上此项目的路径，一般和库项目的包名一致
    artifactId = 'framecore'       //bintray.com 创建的 Package 名
    publishVersion = '1.0.0'         //版本号
    desc = 'app base framework' //版本说明，随意
    website = 'https://github.com/SunshineNumber1/framecore'    //关于这个开源项目的网站，随意
}

